plugins {
    id 'org.springframework.boot' version '2.7.18' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'io.freefair.lombok' version '5.3.0' apply false
}

project.ext {
    dependencyVersions = [
          'mustacheLib'                 : '0.9.10'
    ]
}
ext['log4j2.version'] = '2.17.1'
ext['liquibase.version'] = '4.20.0'

allprojects {
    apply plugin: 'java'

    project.version = '1.0'
    group = 'openrewrite'

    configurations.all {
        exclude module: 'logback-classic'
        exclude module: 'commons-logging'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.codehaus.jackson', module: 'jackson-mapper-asl' // CVE-2017-7525
        exclude group: 'org.glassfish', module: 'javax.el' // CVE-2021-28170
        exclude group: 'org.glassfish.web', module: 'javax.el' // CVE-2021-28170
        exclude group: 'com.netflix.feign'

        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencyLocking {
        //lockMode = LockMode.STRICT // this cause the ./gradlew assemble -x bootJar command to fail on `unlocked` branch -> expected
        lockAllConfigurations()
    }

    tasks.register('resolveAndLockAll') {
        group = "locking"
        notCompatibleWithConfigurationCache("Filters configurations at execution time")
        doFirst {
            assert gradle.startParameter.writeDependencyLocks : "$path must be run from the command line with the `--write-locks` flag"
        }
        doLast {
            configurations.findAll {
                // Add any custom filtering on the configurations to be resolved
                it.canBeResolved
            }.each { it.resolve() }
        }
    }
    repositories {
        mavenCentral()
    }
}

configure( subprojects.findAll { new File( it.projectDir, 'src' ).directory } ) {
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    // Spring boot and cloud release trains
    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2021.0.7'
            mavenBom( 'org.springframework.boot:spring-boot-dependencies:2.7.18' ) {
                bomProperties( [
                      'liquibase.version': '4.20.0',
                      'log4j2.version'   : '2.17.1' //  CVE-2021-44228
                ] )
            }
        }
    }
}